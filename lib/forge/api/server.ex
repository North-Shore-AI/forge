defmodule Forge.API.Server do
  @moduledoc """
  Boots the Forge `/v1` Plug router via Plug.Cowboy.

  Controlled by `:forge, :api_server` config:

      config :forge, :api_server, enabled: true, port: 4102

  Disabled in test to avoid binding real ports.
  """

  use Supervisor

  def start_link(_opts) do
    Supervisor.start_link(__MODULE__, [], name: __MODULE__)
  end

  @impl true
  def init(_) do
    config = Application.get_env(:forge, :api_server, [])

    case Keyword.get(config, :enabled, false) do
      true ->
        port = Keyword.get(config, :port, 4102)
        cowboy = {Plug.Cowboy, scheme: :http, plug: Forge.API.Router, options: [port: port]}
        Supervisor.init([cowboy], strategy: :one_for_one)

      _ ->
        Supervisor.init([], strategy: :one_for_one)
    end
  end
end
